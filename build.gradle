plugins {
    id 'fabric-loom' version '0.6-SNAPSHOT'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven { url = 'https://raw.githubusercontent.com/Devan-Kerman/Devan-Repo/master' }
    maven { url "https://maven.theillusivec4.top" }
    maven { url 'https://dl.bintray.com/ladysnake/libs' }
    maven { url 'https://ladysnake.jfrog.io/artifactory/mods' }
    maven { url 'https://maven.terraformersmc.com/releases' }
    maven { url 'https://maven.blamejared.com' }
    maven { url "https://maven.siphalor.de" }
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://dl.bintray.com/kyrptonaught/customportalapi" }
}


dependencies {
    // Base
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    optionally("net.fabricmc:fabric-loader:${project.loader_version}")
    modApi("net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}") {
        exclude(module: "fabric-dimensions")
        exclude(module: "fabric-biomes")
    }

    //API
    depend("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${cardinal_version}")
    depend( "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${cardinal_version}")
    depend( "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-world:${cardinal_version}")
    depend( "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-chunk:${cardinal_version}")
    // depend("me.shedaniel.cloth:cloth-config-fabric:${config_api_version}")
    depend("me.sargunvohra.mcmods:autoconfig1u:${auto_config_version}")
    depend("com.terraformersmc:modmenu:${mod_menu_version}")
    depend("top.theillusivec4.curios:curios-fabric:${curios_version}")
    depend("vazkii.patchouli:Patchouli:${patchouli_version}")
    depend( "de.siphalor:nbtcrafting-${nbtcrafting_version}")
    depend("net.kyrptonaught:customportalapi:${portal_api_version}")
    optionally("me.shedaniel:RoughlyEnoughItems-api:${rei_version}")
    modRuntime("me.shedaniel:RoughlyEnoughItems:${rei_version}")

    // .dev .jar doesn't include included deps, so I have to add them for in IDE to work
    compile files("Library-Of-Exile-1.16.5-1.0.8-dev.jar")
    compile files("World-Of-Exile-1.16.5-1.2.0-dev.jar")
}

def depend(str) {
    dependencies.modApi(str) {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "io.github.prospector")
        exclude(module: "config-2")
    }
    dependencies.include(str) {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "io.github.prospector")
        exclude(module: "config-2")
    }
}

def optionally(str) {
    runtime(str)
    dependencies.modCompileOnly(str) {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "io.github.prospector")
        exclude(module: "config-2")
    }
}

def runtime(str) {
    dependencies.modRuntime(str) {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "io.github.prospector")
        exclude(module: "config-2")
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

sourceSets {
    main {
        resources {
            srcDirs += project.file("src/generated/resources")
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// CURSEFORGE STUFF

curseforge {

    def curseProp = new Properties()
    File secretPropsFile = file("key.properties")
    curseProp.load(secretPropsFile.newInputStream())

    project {
        apiKey = curseProp.getProperty('curseKey')
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')
        addGameVersion("${project.minecraft_version}")
        addGameVersion("Fabric")
        addGameVersion("Java 8")

        relations {
            requiredDependency 'fabric-api'
            requiredDependency 'library-of-exile'
            requiredDependency 'dungeons-of-exile'
            requiredDependency 'curios-fabric'
            requiredDependency 'patchouli-fabric'
        }
        mainArtifact(remapJar)

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

